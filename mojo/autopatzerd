#!/usr/bin/perl

use strict;
use warnings;

use lib 'lib';

use EV; # we need to use EV in order for AnyEvent and Mojolicious to play nicely together
use AnyEvent;
use AnyEvent::Handle;
use Autopatzer;
use Autopatzer::Button;
use Mojolicious::Lite;
use Mojo::JSON qw(encode_json decode_json);
use Chess::Rep;
use Try::Tiny;
use Storable qw(dclone);

my @clients;

my $ap = Autopatzer->new(
    device => '/dev/ttyACM0',
    cb => sub {
        my ($self) = @_;
        send_changes();
    },
);

my $button = Autopatzer::Button->new(
    pin => 21, # physical pin 40, BCM pin 21, WiringPi pin 29
    cb => sub {
        my $m = encode_json({op => 'button'});
        print ">> $m\n";
        $_->send($m) for @clients;
    },
);

# use AnyEvent to read lines from serial device
my $handle = AnyEvent::Handle->new(
    fh => $ap->{fh},
    on_read => sub {
        my ($handle) = @_;
        my $data = substr($handle->rbuf, 0, length($handle->rbuf), '');
        $ap->handle_data($data);
    },
);

# TODO: move most of this into Autopatzer.pm and just get $ap->changes or something,
# also use same in moveShown instead of duplicating it
sub send_changes {
    my @lost;
    my @gained;
    my $move = '';

    return if !$ap;

    try {
        my $maybemove = $ap->moveShown;
        $move = dclone($ap->{game})->go_move($maybemove)->{san} if $maybemove;
    } catch {
        warn "$_" unless $_ =~ /Illegal move/;
    };

    for my $rank (1..8) {
        for my $file ('a'..'h') {
            my $sqr = "$file$rank";
            if ($ap->{occupied}{$sqr} && !$ap->{game}->get_piece_at($sqr)) {
                push @gained, $sqr;
            } elsif ($ap->{game}->get_piece_at($sqr) && !$ap->{occupied}{$sqr}) {
                push @lost, $sqr;
            }
        }
    }

    my $m = encode_json({op => 'board', lost => \@lost, gained => \@gained, move => $move});
    print ">> $m\n";
    $_->send($m) for @clients;
}

websocket '/ws' => sub {
    my $c = shift;

    push @clients, $c;

    $c->inactivity_timeout(3600);

    # send the client an initial "board" message explaining the
    # changes to the board, if any (i.e. lost/gained squares)
    # (this goes to all clients but we don't care)
    send_changes();

    my @timers;
    my $ping_timer = Mojo::IOLoop->recurring(60 => sub {
        my $m = encode_json({op => 'ping'});
        $c->send($m);
    });

    $c->on(message => sub {
        my ($c, $msg_str) = @_;
        print "<< $msg_str\n";
        try {
            my $msg = decode_json($msg_str);

            if ($msg->{op} eq 'reset') {
                Mojo::IOLoop->remove($_) for @timers;
                $ap->reset;
                for my $m (@{ $msg->{moves}||[] }) {
                    $ap->moveWithMotors($m);
                }
                send_changes();
            } elsif ($msg->{op} eq 'play') {
                if ($ap->moveShown) {
                    $ap->moveWithoutMotors($msg->{move});
                } else {
                    $ap->moveWithMotors($msg->{move});
                }
            } elsif ($msg->{op} eq 'wiggle') {
                $ap->wiggle;
            } elsif ($msg->{op} eq 'ping') {
                # do nothing
            } else {
                my $m = encode_json({op=>'error',error=>'No op specified.'});
                $c->send($m);
            }
        } catch {
            warn "$_";
            my $m = encode_json({op=>'error',error=>$_});
            $c->send($m);
        };
    });

    $c->on(finish => sub {
        Mojo::IOLoop->remove($_) for (@timers, $ping_timer);
        @clients = grep { $_ != $c } @clients;
    });
};

# XXX: listen on port 3001
if (@ARGV == 1 && $ARGV[0] eq 'daemon') {
    push @ARGV, '--listen', 'http://*:3001';
}

app->start;
